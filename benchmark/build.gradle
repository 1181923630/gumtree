plugins {
    id 'application'
}

dependencies {
    implementation project(':gen.jdt')
}

uploadArchives.enabled = false
jar.enabled = false

task computeResults(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'com.github.gumtree.benchmark.Defects4JLauncher'
    args "${project.projectDir}/defects4j", "${project.buildDir}/reports/defects4j.csv"
}

task plotResults(type: Exec) {
    dependsOn computeResults
    commandLine "python3", "${project.projectDir}/src/main/python/plotDefects4J.py", "${project.buildDir}/reports/defects4j.csv"
}

task testSizeRegressions(type: Exec) {
    dependsOn computeResults
    commandLine "python3", "${project.projectDir}/src/main/python/testRegressions.py", "${project.projectDir}/defects4j.csv", "${project.buildDir}/reports/defects4j.csv", "size"
}

task testRuntimeRegressions(type: Exec) {
    dependsOn computeResults
    commandLine "python3", "${project.projectDir}/src/main/python/testRegressions.py", "${project.projectDir}/defects4j.csv", "${project.buildDir}/reports/defects4j.csv", "runtime"
}

task runBenchmark() {
    dependsOn plotResults, testSizeRegressions, testRuntimeRegressions
}

build.finalizedBy plotResults, testSizeRegressions